plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id 'jacoco'
    alias(libs.plugins.nexus.publish)
}

version = '3.1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    implementation(libs.slf4j.api)
    testImplementation(libs.junit4)
    testRuntimeOnly(libs.logback)
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(11)
        vendor = JvmVendorSpec.ADOPTIUM
    }
    withSourcesJar()
    withJavadocJar()
}

javadoc {
    failOnError = false
}

sourcesJar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

jar {
    manifest {
        attributes("Automatic-Module-Name": "org.omegat.vldocking")
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) { publication ->
            groupId = 'org.omegat'
            artifactId 'vldocking'
            from components.java

            pom {
                name = 'vldocking'
                description = 'vldocking library'
                url = 'https://omegat.org'
                scm {
                    connection = "scm:git:https://github.com/omegat-org/vldocking"
                    developerConnection = "scm:git:https://github.com/omegat-org/vldocking"
                    url = "https://github.com/omegat-org/vldocking/"
                }
                licenses {
                    license {
                        name = 'The GNU Lesser General Public License, Version 3.0'
                        url = 'https://www.gnu.org/licenses/lgpl-3.0.html'
                    }
                }
                developers {
                    developer {
                        id = 'omegat'
                        name = 'OmegaT Developers'
                        email = 'info@omegat.org'
                    }
                }
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
    if (!findProperty("signing.keyId")) {
        useGpgCmd()
    }
}
def sonatypeUsername = project.hasProperty("sonatypeUsername")? project.property('sonatypeUsername') : System.getenv('SONATYPE_USER')
def sonatypePassword = project.hasProperty("sonatypePassword")? project.property('sonatypePassword') : System.getenv('SONATYPE_PASS')
if (sonatypeUsername != null && sonatypePassword != null) {
    nexusPublishing {
        repositories{
            sonatype {
                nexusUrl.set(new URI("https://s01.oss.sonatype.org/service/local/"))
                snapshotRepositoryUrl.set(new URI("https://s01.oss.sonatype.org/content/repositories/snapshots/"))
                username.set(sonatypeUsername)
                password.set(sonatypePassword)
            }
        }
    }
}
